      subroutine cphf_solve(rtdb)
      implicit none
#include "mafdecls.fh"
#include "global.fh"
#include "bas.fh"
#include "geom.fh"
#include "rtdb.fh"
#include "inp.fh"
#include "cscfps.fh"
#include "sym.fh"
#include "util.fh"
#include "cscf.fh"
      integer rtdb              ! [input] database handle
      double precision eone,etwo,enrep,energy
      integer g_grad, g_work
      integer max_precond_iter,max_precond_sub
      double precision precond_acc
      integer precond_iter
      logical ga_iter_lsolve
      external ga_iter_lsolve
      logical oprint_solve
c$$$  logical ga_it_ls_mrhs
c$$$  external ga_it_ls_mrhs
      external rohf_hessv_precond,rohf_hessv
      external uhf_precond,uhf_hessv
      logical oprint, oprint_conv
c     
      character*(nw_max_path_len) cphf_rhs
      character*(nw_max_path_len) cphf_sol
      logical file_write_ga,file_read_ga
      external file_write_ga,file_read_ga
      integer vlen, nvirt, nmos(2), nbf_vecs, nsets
      character*255 title_vecs, basis_vecs
      logical movecs_read_header
      external movecs_read_header
c
c     Print levels assume that CPHF is being invoked from within
c     another application.  By default be informative.
c     At low print, just indicate convergence or failure.
c
      oprint = util_print('information',print_default) .and.
     $     ga_nodeid() .eq. 0
      oprint_conv = util_print('information',print_low) .and.
     $     ga_nodeid() .eq. 0
      oprint_solve = util_print('solve',print_default)
c
c     Must use the same vectors as the converged SCF
c
      if (.not. rtdb_cget(rtdb, 'scf:input vectors', 1, movecs_in))
     $     call errquit('cphf: no SCF MO vectors',0)
c     
      call scf_init(rtdb)
      call int_normalize(basis)
      call int_init(rtdb,1,basis)
      call schwarz_init(geom, basis)
      oskel=sym_number_ops(geom).gt.0
      call int_1e_cache_ga(basis,oskel)
c
c     Get the MO vectors
c
      if(.not.movecs_read_header(movecs_in,title_vecs,basis_vecs,
     $     nbf_vecs,nsets,nmos,2))
     $     call errquit('cphf: problem with movecs_read_header',1)
      if(nbf.ne.nbf_vecs)call errquit('cphf: MO vectors mismatch?',0)
      nmo = nmos(1)
c
      if (nmo .ne. nbf) then
         if (.not. ga_destroy(g_movecs(1))) call errquit('cphf: ga',1)
         if (.not. ga_create(MT_DBL, nbf, nmo, 'scf_v_g: tmp MOs a',
     $        1, 1, g_movecs(1))) call errquit('scf_v_g: tmp MOs', 0)
         if (scftype .eq. 'UHF') then
            if (.not. ga_destroy(g_movecs(2)))call errquit('cphf: ga',2)
            if (.not. ga_create(MT_DBL, nbf, nmo, 'scf_v_g: tmp MOs b',
     $           1, 1, g_movecs(2))) call errquit('scf_v_g: tmp MOs', 0)
         endif
      endif
c         
      call scf_movecs_read
c
      if (scftype.eq.'UHF') then 
         vlen = nalpha*(nmo-nalpha) + nbeta*(nmo-nbeta)
      else if (scftype .eq. 'RHF' .or. scftype.eq.'ROHF') then
         nvirt = nmo - nclosed - nopen
         vlen = nclosed*(nopen+nvirt) + nopen*nvirt
      else
         call errquit('cphf: unknown SCF type',0)
      endif
c
      lshift=0.0d0
      if (.not. rtdb_get(rtdb, 'cphf:thresh', mt_dbl, 1, 
     $     precond_tol)) precond_tol=1d-5
      if (.not. rtdb_get(rtdb, 'cphf:maxiter', mt_int, 1,
     $     max_precond_iter)) max_precond_iter = 100
c
      pflg=2
      max_precond_sub = 20
      if (oprint) then
         write(6,170) scftype
 170     format('    scftype = ',a)
         if (scftype .eq. 'RHF' .or. scftype.eq.'ROHF') then
            write(6,171) nclosed, nopen
 171        format('    nclosed = ', i4/
     $             '      nopen = ', i4)
         else
            write(6,172) nalpha, nbeta
 172        format('     nalpha = ', i4/
     $             '      nbeta = ', i4)
         endif
         write(6,173) vlen, precond_tol, max_precond_iter
 173     format('  variables = ', i8/
     $          '  tolerance = ', 1p,d9.1,0p/
     $          '  max. iter = ', i4/)
      endif
c
      if(.not.ga_create(mt_dbl,vlen,1,'cphf_solve: rhs',1,1,g_grad))
     $     call errquit('cphf_solve: ga_cre grad',vlen)
c
      call util_file_name('cphf_rhs',.true.,.true.,cphf_rhs)         
      if(.not.file_read_ga(cphf_rhs, g_grad))
     $     call errquit('cphf_solve: could not read cphf_rhs',1)
c
      if(.not.ga_create(mt_dbl,vlen,1,'cphf_solve: grad',
     $     1,1,g_work))call errquit('cphf_solve: ga_cre grad',0)
c
c     Compute necessary Fock matrices
c
      if(scftype .eq. 'UHF')then
         call uhf_init(rtdb)
         call uhf_energy(g_movecs,eone,etwo,enrep,energy,g_work)
      else
         call rohf_init(rtdb)
         call rohf_energy(g_movecs,eone,etwo,enrep,energy,g_work)
      endif
c
      if (sqrt(ga_ddot(g_work, g_work)) .gt. 1d-4) 
     $     call errquit('cphf_solve:the available MOs do not satisfy'//
     $     ' the SCF equations',0)
c
      call ga_zero(g_work)      ! Essential to get default initial guess
c
c
      if (scftype .eq. 'UHF') then
         if(.not.ga_iter_lsolve(vlen, max_precond_iter,
     $        max_precond_sub, precond_tol, 
     $        uhf_precond, uhf_hessv, .false., oprint_solve, 
     $        g_grad, g_work, precond_acc, precond_iter))
     $        call errquit('cphf did not converge',precond_iter)
      else 
         if(.not.ga_iter_lsolve(vlen, max_precond_iter,
     $        max_precond_sub, precond_tol, 
     $        rohf_hessv_precond, rohf_hessv, .false., oprint_solve, 
     $        g_grad, g_work, precond_acc,precond_iter))
     $        call errquit('cphf did not converge', precond_iter)
      endif
c
      if (oprint_conv) then
         write(6,174) precond_acc, precond_iter
 174     format(/' CPHF converged to ',1p,d9.1,0p,' in ', i4, ' iters'/)
         call util_flush(6)
      endif
c
c$$$  if(.not.ga_it_ls_mrhs(cuhf_vlen,1,max_iter,
c$$$  $        max_sub,accuracy, 
c$$$  $        uhf_precond,uhf_hessv,oprint, 
c$$$  $        g_grad,g_work,precond_acc,precond_iter))
c$$$  $        call errquit('cphf did not converge',1)
c$$$  pol=ga_ddot(g_work,g_grad)
c$$$  if(ga_nodeid().eq.0)then
c$$$  write(6,911) pol
c$$$  911        format(//,15x,40('-'),
c$$$  $           /,15x,'UHF Polarizability is:',f16.6,
c$$$  $           /,15x,40('-'),/)
c$$$  endif
c
      call util_file_name('cphf_sol',.true.,.true.,cphf_sol)
      if(.not.file_write_ga(cphf_sol,g_work))
     $     call errquit('cphf_solve: could not write cphf_sol',1)
      if(.not.ga_destroy(g_work))call errquit(
     $     'cphf_solve: could not destroy work array',1)
      if(.not.ga_destroy(g_grad))call errquit(
     $     'cphf_solve: could not destroy gradient handle',1)
c
      call int_1e_uncache_ga()
      call schwarz_tidy()
      call int_terminate()
      if(scftype .eq. 'UHF') then
         call uhf_tidy(rtdb)
      else if(scftype .eq. 'RHF' .or. scftype.eq.'ROHF') then
         call rohf_tidy
      endif
      call scf_tidy(rtdb)
c
      return
c
      call cphf_poliz(rtdb)     ! Never executed.
c
      end
