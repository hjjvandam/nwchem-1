      logical function task_freq(rtdb)
c     
c     Central difference calculation of the hessian using
c     the generic energy/gradient interface.  Uses a routine inside
c     stepper to do the finite difference ... this needs to be
c     cleaned up to be independent of stepper.
c
c     Also will be hooked up to analytic methods as they are available.
c
c     Since this routine is directly invoked by application modules
c     no input is processed in this routine.
c
c     RTDB input parameters
c     ---------------------
c     task:theory
c
c     RTDB output parameters
c     ----------------------
c     task:hessian file name (string) - name of file containing hessian
c     task:status (logical)      - T/F on success/failure
c     task:cputime
c     task:walltime
c
*
* $Id: task_freq.F,v 1.16 1998-08-26 00:27:52 d3e129 Exp $
*
      implicit none
#include "mafdecls.fh"
#include "rtdb.fh"
#include "geom.fh"
#include "stdio.fh"
#include "global.fh"
#include "util.fh"
c
      logical task_hessian
      external task_hessian
c     
      integer rtdb
c     
      integer nat, geom
      logical status
      character*(nw_max_path_len) filehess
c     
      double precision cpu, wall
c 
      logical      ignore
c    
      call ecce_print_module_entry('task frequencies')
      cpu  = util_cpusec()
      wall = util_wallsec()
c
      if (.not. rtdb_put(rtdb, 'task:status', mt_log, 1, .false.))
     $     call errquit('task_freq: failed to invalidate status',0)
      if (ga_nodeid().eq.0 .and.
     $    util_print('task_freq', print_low)) then
        write(6,*)
        write(6,*)
        call util_print_centered(6,
     $      'NWChem Nuclear Hessian and Frequency Analysis',
     $      40,.true.)
        write(6,*)
      endif

      status = task_hessian(rtdb)
      if (.not.status) call errquit
     &    ('task_freq: task_hessian failed',911)
      ignore = rtdb_parallel(.false.)
      if ((ga_nodeid()).eq.0) then
        if (.not. rtdb_cget(rtdb, 'task:hessian file name', 1,
     $      filehess)) call errquit
     $      ('task_freq: failed reading hessian filename from rtdb',0)
c
c     create/load reference geometry
c
        if (.not.geom_create(geom,'geometry')) call errquit
     $      ('task_freq:geom_create failed?',1)
        if (.not.geom_rtdb_load(rtdb,geom,'geometry'))
     $      call errquit
     $      ('task_freq:geom_rtdb_load failed?',2)
        if (.not. geom_ncent(geom,nat)) call errquit
     $      ('task_freq:geom_ncent failed?',3)
        if (.not. geom_destroy(geom)) call errquit
     $      ('task_freq:geom_destroy failed?',911)
        call vib_vib(rtdb,filehess,.true.,
     $      0,.false.,0,.false.,nat)
      endif
      call ga_sync()
      ignore = rtdb_parallel(.true.)
c
      cpu  = util_cpusec() - cpu
      wall = util_wallsec() - cpu
c
      if (.not. rtdb_put(rtdb, 'task:cputime', mt_dbl, 1, cpu))
     $     call errquit('task_freq: failed storing cputime',0)
      if (.not. rtdb_put(rtdb, 'task:walltime', mt_dbl, 1, wall))
     $     call errquit('task_freq: failed storing walltime',0)
      if (.not. rtdb_put(rtdb, 'task:status', mt_log, 1, .true.))
     $     call errquit('task_freq: failed to set status',0)
c
c
      call ecce_print1('cpu time', mt_dbl, cpu, 1)
      call ecce_print1('wall time', mt_dbl, wall, 1)
      task_freq = status
      if (task_freq) then
        call ecce_print_module_exit('task frequencies', 'ok')
      else
        call ecce_print_module_exit('task frequencies', 'failed')
      endif
c
c
      end
