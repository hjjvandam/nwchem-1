!-----------------------------------------------------------------------
!>
!> \brief Guess the molecular orbitals
!>
!> We use the "standard" approach to guess the molecular orbitals
!> based on the sum of atomic densities. See also DOI:
!> <a href="https://doi.org/10.1002/jcc.20393">10.1002/jcc.20393</a>.
!>
      subroutine noft_guess_mos(rtdb,noft_instances,noft_params,
     &                          noft_operators,noft_wavefunction,eigval)
      implicit none
#include "global.fh"
#include "errquit.fh"
#include "mafdecls.fh"
!>    The runtime database handle
      integer,                    intent(in)    :: rtdb
!>    The calculation instances
      type(noft_instances_tp),    intent(in)    :: noft_instances
!>    The parameters of the calculation
      type(noft_parameter_tp),    intent(in)    :: noft_params
!>    The calculation operators
      type(noft_operators_tp),    intent(in)    :: noft_operators
!>    The wavefunction
      type(noft_wavefunction_tp), intent(inout) :: noft_wavefunction
!>    The molecular orbital eigenvalues
      real(kind=dp),              intent(out)   :: eigval(:)
!
!     Local
!
      character(len=22), parameter :: pname = "noft_guess_mos"
      integer :: g_ao_ao
      integer :: g_s12 ! \$S^{-1/2}\$
      integer :: g_s   ! \$S\$
      integer :: mo_a
      integer :: nao
      integer :: nelec
      integer :: nmo
      integer :: geometry
      integer :: basis
      real(kind=dp) :: aa, bb
!
      basis    = noft_instances%basis
      geometry = noft_instances%geom
      g_s12    = noft_wavefunction%so
      g_s      = noft_operators%ao_s
      mo_a     = noft_wavefunction%mo_a
      nao      = noft_params%nao
      nelec    = noft_params%ne_a
     &         + noft_params%ne_b
      nmo      = noft_params%nmo
      aa       = 1.0_dp
      bb       = 0.0_dp
!
      if (.not.ga_create(MT_DBL,nao,nao,"g_ao_ao",-1,-1,g_ao_ao)) then
        call errquit(pname//" allocate g_ao_ao failed",10,GA_ERR)
      endif
      call ga_zero(g_ao_ao)
      call guess_dens(rtdb,geometry,basis,g_ao_ao)
      call rhf_dens_to_mo(rtdb,geometry,basis,nelec,nao,nmo,1.0d-7,
     &                    g_ao_ao,mo_a,.false.,eigval)
!
!     Now we have the MOs in an non-orthogonal basis, but we need them
!     in terms of the orthogonal basis. So we need the solution of
!     \$S^{-1/2} X = M\$ where \$M\$ are the MOs we have now, and
!     \$S^{-1/2}\$ is the orthogonalize transformation. We can also
!     write this as \$X = (S^{-1/2})^T S M\$ because then 
!     \$S^{-1/2}X = S^{-1/2}(S^{-1/2})^T S M = S^{-1}S M = M\$
!
      call ga_dgemm('n','n',nao,nmo,nao,aa,g_s,mo_a,bb,g_ao_ao)
      call ga_dgemm('t','n',nmo,nao,nao,aa,g_s12,g_ao_ao,bb,mo_a)
!
      call ga_copy(noft_wavefunction%mo_a,noft_wavefunction%mo_b)
      if (.not.ga_destroy(g_ao_ao)) then
        call errquit(pname//" destroy g_ao_ao failed",20,GA_ERR)
      endif
!
      end subroutine noft_guess_mos
!
!-----------------------------------------------------------------------
