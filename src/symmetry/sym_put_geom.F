      subroutine sym_put_in_geom(geom, nat_uniq, nat_new,
     $     tags_new, uniq_cent, coords_new, l_map, k_map, nops)
C$Id: sym_put_geom.F,v 1.1 1995-12-13 01:28:40 d3g681 Exp $
      implicit none

#include "mafdecls.fh"
#include "geom.fh"
#include "geomP.fh"

      integer geom,nat_uniq,nat_new,i,j,l_map, k_map, nops
      character*16 tags_new(nat_new)
      integer uniq_cent(nat_uniq)
      double precision coords_new(3,nat_new)
      double precision charge_new(max_cent)
c
c     tags_new(1:nat_new) = tags for full list of centers
c     nat_uniq = no. of symmetry unique atoms = no. of atoms in the geom
c     nat_new = no. of atoms in full list
c     coords_new = list of FRACTIONAL coordinates that must be
c                  converted to CARTESIAN coordinates before insertion
c     l_map/k_map = MA handle/index to atom symmetry map
c     nop = no. of operations in the group
c
c     Having generated the full list of centers from the unique
c     list need to copy the tags and charges over to geom.  The tags 
c     are dynamically allcoated as CHAR*1 so need a subroutine call
c     to reference them to CHAR*16.  Also, we don't have the new 
c     charges at all ... they need to be generated by matching the new
c     labels agains the old ones ... this should also be used for
c     additional fields (e.g., masses, nuclear dipoles).
c
c     charges (and other fields ... must do these BEFORE the tags)
c     Note that cannot rely on geom_tag_to_element since user
c     may have specified pseudo-potential, or it may be a dummy
c     center with zero or fractional charge.
c
      do i=1,nat_new
c
c     Match tag of new center against existing atom tag
c
         do j = 1, nat_uniq
            if (tags(j,geom) .eq. tags_new(i)) then
               charge_new(i) = charge(j,geom)
c
c     put other fields here
c
            endif
         enddo
      enddo
c
      do i = 1, nat_new
         charge(i,geom) = charge_new(i)
      enddo
c
c     Copy new tags and coordinates over last
c
      do i=1,nat_new
         tags(i,geom)=tags_new(i)
      enddo
      call dgemm('n', 'n', 3, nat_new, 3, 1.0d0, amatrix(1,1,geom), 3,
     $     coords_new, 3, 0.0d0, coords(1,1,geom), 3)
c
      ncenter_unique(geom) = nat_uniq
      ncenter(geom) = nat_new
      do i=1,nat_uniq
        unique_cent(i,geom)=uniq_cent(i)
      enddo
c
      sym_center_map_handle(geom) = l_map
      sym_center_map_index(geom)  = k_map
      sym_num_ops(geom) = nops
c
      end
